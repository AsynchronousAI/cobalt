// The Computer Language Benchmarks Game
// http://shootout.alioth.debian.org/
// contributed by Mike Pall

function A(i, j) {
  var ij = i+j-1
  return 1.0 / (ij * (ij-1) * 0.5 + i)
}

function Av(x, y, N) {
  for( i=1,N ) {
    var a = 0
    for( j=1,N ) 
      a = a + x[j] * A(i, j)
    y[i] = a
  }
}

function Atv(x, y, N) {
  for( i=1,N ) {
    var a = 0
    for( j=1,N )
      a = a + x[j] * A(j, i) 
    
    y[i] = a
  }
}

function AtAv(x, y, t, N) {
  Av(x, t, N)
  Atv(t, y, N)
}

N = tonumber(arg && arg[1]) || 100
u, v, t = {}, {}, {}
for( i=1,N ) { u[i] = 1 }

for( i=1,10 ) { AtAv(u, v, t, N) AtAv(v, u, t, N) }

var vBv, vv = 0, 0
for( i=1,N ) {
  var ui, vi = u[i], v[i]
  vBv = vBv + ui*vi
  vv = vv + vi*vi
}
io.write(string.format("%0.9f\n", math.sqrt(vBv / vv)))